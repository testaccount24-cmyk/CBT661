         MACRO
&NAME    WTOL  &ROUTCDE=,&MF=I,&DESC=,&MODE=WTO
.*--------------------------------------------------------------------*
.*
.*       THIS MACRO IS USED TO GENERATE WTO MESSAGES WITH LABELS
.*       ON FIELDS WITHIN THE MESSAGE THAT THE USER WISHES TO MODIFY.
.*       THE FORMATS OF THE MACRO ARE...
.*
.*                      WTOL MF=(E,ADR)
.*                          - OR -
.*    LABEL WTOL 'TEXT1',(LABEL1,NN),'TEXT2',(LABEL2,NN),MF=(G,ADR)
.*                          - OR -
.*    LABEL WTOL 'TEXT1',(LABEL1,NN),'TEXT2',(LABEL2,NN),MF=L
.*                          - OR -
.*    LABEL WTOL 'TEXT1',(LABEL1,NN,'F'),'TEXT2',(LABEL2,NN,'F'),MF=L
.*
.*  ===> WHERE..... NN IS THE LENGTH OF THE LABELED FIELD
.*            ..... F IS THE FILL CHARACTER DEFAULTING TO BLANKS
.*            ..... ROUTCDE, DESC, MF=E, AND MF=L ARE SUPPORTED
.*
.*  ===> NOTE ..... MF=E IS SUPPORTED DIRECTLY BY WTO
.*            ..... MF=G IS SUPPORTED FOR USE IN REENTRANT CODE.
.*                  THE LABEL FIELDS ARE DSECT REFERENCES. THE
.*                  ADDRESS SPECIFIED MUST POINT TO A GETMAIN AREA.
.*
.*            VALIDITY CHECK MODE PARAMETER
.*
.*       THE MODE PARAMETER ALLOWS THE USE OF THIS MACRO
.*       TO GENERATE LISTS OR IN-LINE CODE FOR THE
.*       COMMANDS (SVC 34), OR WTL (SVC 36).
.*       IF MODE=COMMAND OR MODE=WTL ARE SPECIFIED, THE
.*       ROUTING AND DESCRIPTOR OPERANDS ARE INVALID.
.*
.*       COPYRIGHT (C) 1989, TAURUS SOFTWARE CONSULTANTS
.*
.*       CHANGE LOG:
.*             06/12/89, PMCF ADDED MF=(G,ADR) SUPPORT.
.*--------------------------------------------------------------------*
         LCLA  &LISTN,&N,&SVC,&V#,&VL#
         LCLB  &B(16),&D(16),&WTO,&COMM,&LOG,&MFG,&TSO
         LCLC  &GNAME,&LAB,&DE(2),&VARS(16),&L,&TEMP,&XNAM
         GBLB  &@@CONVT
.*
         AIF  ('&MF(1)' NE 'E').NOTE
&NAME    WTO  &SYSLIST(1),ROUTCDE=&ROUTCDE,DESC=&DESC,MF=&MF
         MEXIT
.NOTE    ANOP
&WTO     SETB ('&MODE' EQ 'WTO')
&COMM    SETB ('&MODE' EQ 'COMMAND')
&LOG     SETB ('&MODE' EQ 'LOG')
&TSO     SETB ('&MODE' EQ 'TSO')
         AIF  (&WTO OR &COMM OR &LOG OR &TSO).MODEOK1
         AGO   .E0
.MODEOK1 ANOP
         AIF   (NOT &WTO AND                                           X
               ('&ROUTCDE' NE '' OR '&DESC' NE '')).E6
         AIF   (NOT &WTO).NOTWTO
&SVC     SETA  35
         AGO   .SVCSET
.NOTWTO  ANOP
         AIF   (NOT &COMM).NOTCMD
&SVC     SETA  34
         AGO   .SVCSET
.NOTCMD  ANOP
         AIF   (NOT &LOG).NOTLOG
&SVC     SETA  36
         AGO   .SVCSET
.NOTLOG  ANOP
         AIF   (NOT &TSO).NOTTSO
&SVC     SETA  93
         AGO   .SVCSET
.NOTTSO  ANOP
.SVCSET  ANOP
.*
.*       VALIDITY CHECK KEYWORD PARAMETERS
.*
&LISTN   SETA  1
&XNAM    SETC  '&NAME'
         AIF   ('&MF' EQ 'I' OR '&NAME' NE '').NAMESET
         AIF   (NOT &MFG).E1
&XNAM    SETC  '&GNAME.E'
.NAMESET ANOP
         AIF   (N'&SYSLIST EQ 0).E2
         AIF   ('&ROUTCDE' EQ '').NORTE1
.*
.*       VALIDITY CHECK ROUTCDE AND SET VALUE
.*
.RTELOO  ANOP
&N       SETA  &ROUTCDE(&LISTN)
         AIF   (&N LT 1 OR &N GT 16).E3
&B(&N)   SETB  1
&LISTN   SETA  &LISTN+1
         AIF   (N'&ROUTCDE GE &LISTN).RTELOO
.NORTE1  ANOP
.*
.*       VALIDITY CHECK DECSRIPTOR CODES AND SET VALUES
.*
         AIF   ('&DESC' EQ '').NODESC
&LISTN   SETA  1
.DESLOO  ANOP
&N       SETA  &DESC(&LISTN)
         AIF   (&N LT 1 OR &N GT 16).E5
&D(&N)   SETB  1
&LISTN   SETA  &LISTN+1
         AIF   (N'&DESC GE &LISTN).DESLOO
.NODESC  ANOP
.*
.*       VALIDATE MF=(G,ADR)
.*
         AIF   ('&MF(1)' NE 'G').NOTG
         AIF   (N'&MF NE 2).E7
&MFG     SETB  1
.NOTG    ANOP
.*
.*       GENERATE INLINE MESSAGE LENGTH AND FLAGS FIELD
.*
&GNAME   SETC  '$TS'.'&SYSNDX'
         AIF   ('&MF' EQ 'L').MFL
         CNOP  0,4                 ALIGN LIST
         AIF   (NOT &COMM).NOTCOMM
         SLR   R0,R0               CONSOLE ID
.NOTCOMM ANOP
         AIF   (NOT &MFG).NOTRENT
&XNAM    BAL   1,&GNAME.B
         AGO   .MFLRTN
.NOTRENT ANOP
&XNAM    BAL   1,&GNAME.D
         AGO   .MFLRTN
.MFL     ANOP
&XNAM    DS    0F                  ALIGN MESSAGE
.MFLRTN  ANOP
         DC    AL2(&GNAME.A-*)  MESSAGE LENGTH
         AIF   ('&ROUTCDE' EQ '' AND '&DESC' EQ '').NOMCS
         DC    B'1000000000000000' MCS FLAGS
         AGO   .R1
.NOMCS   ANOP
         DC    AL2(0)              NO MCS FLAGS
.R1      ANOP
.*
.*       GENERATE MESSAGE TEXT AND LABEL VARIABLE FIELDS
.*
&LISTN   SETA  1
&V#      SETA  0
.XPAND   AIF   (N'&SYSLIST(&LISTN) EQ 1).CONST
         AIF   (N'&SYSLIST(&LISTN) GT 3).E4
         AIF   (NOT &MFG).ULAB
&LAB     SETC  '&GNAME'.'&V#'
&V#      SETA  &V#+1
&VARS(&V#)  SETC  '&SYSLIST(&LISTN,1)'
         AGO   .LABSET
.ULAB    ANOP
&LAB     SETC  '&SYSLIST(&LISTN,1)'
.LABSET  ANOP
         AIF   ('&SYSLIST(&LISTN,3)' EQ '').NULLC
&LAB     DC    CL&SYSLIST(&LISTN,2).&SYSLIST(&LISTN,3)
         AGO   .CEND
.NULLC   ANOP
&LAB     DC    CL&SYSLIST(&LISTN,2).' '
         AGO   .CEND
.CONST   ANOP
         DC    C&SYSLIST(&LISTN)
.CEND    ANOP
&LISTN   SETA  &LISTN+1
         AIF   (N'&SYSLIST GE &LISTN).XPAND
&GNAME.A EQU   *
.*
.*       GENERATE DESCRIPTOR AND ROUTING CODE FIELDS
.*
         AIF   ('&ROUTCDE' EQ '' AND '&DESC' EQ '').NORTE2
&DE(1)   SETC  '&D(1)&D(2)&D(3)&D(4)&D(5)&D(6)&D(7)&D(8)'
&DE(2)   SETC  '&D(9)&D(10)&D(11)&D(12)&D(13)&D(14)&D(15)&D(16)'
         DC    B'&DE(1).&DE(2)'    DESCRIPTOR CODES
&DE(1)   SETC  '&B(1)&B(2)&B(3)&B(4)&B(5)&B(6)&B(7)&B(8)'
&DE(2)   SETC  '&B(9)&B(10)&B(11)&B(12)&B(13)&B(14)&B(15)&B(16)'
         DC    B'&DE(1).&DE(2)'    ROUTING CODES
.NORTE2  ANOP
         AIF   ('&MF' EQ 'L').NOSVC
         AIF   (NOT &MFG).SVC
&GNAME.C DC    AL2(*-&XNAM-5)
&GNAME.B DS    0H
         ST    R2,12(,R13)
         IHBINNRA &MF(2)
         LA    R15,&XNAM+4
         ICM   R14,B'0011',&GNAME.C
        $EX    R14,(MVC,0(0,R1),0(R15))
&VL#     SETA  0
&L       SETC  'L'''
.CONSTLOOP ANOP
         LA    R15,&GNAME.&VL#-&XNAM-4
         LA    R15,0(R15,R1)
         LA    R14,&L&GNAME.&VL#-1
&TEMP    SETC  '&VARS(&VL#+1)'
         AIF   ('&TEMP'(1,1) EQ '(').VREG
         LA    R2,&TEMP
         AGO   .VEX
.VREG    ANOP
         LR    R2,&TEMP
.VEX     ANOP
        $EX    R14,(MVC,0(0,R15),0(R2))
&VL#     SETA  &VL#+1
         AIF   (&VL# LT &V#).CONSTLOOP
         L     R2,12(,R13)             RESTORE WORK REGISTER
.SVC     ANOP
         DS    0H                      ALIGN AFTER LIST
         AIF   (NOT &TSO).DOSVC
         LH    0,0(,R1)                LENGTH OF TEXT
         LA    R1,4(,R1)               --> TO TEXT FIELD
.DOSVC   ANOP
&GNAME.D SVC   &SVC                    ISSUE REQUESTED SVC
         AIF   (&@@CONVT).NOSVC        SKIP THE NEXT INST.
.*                                     IF WE HAVE ONE ALREADY
&@@CONVT SETB  1
         B     &GNAME.E
        $EX    GENERATE
&GNAME.E DS    0H
.NOSVC   ANOP
         MEXIT
.E0      MNOTE 8,'MODE PARAMETER INVALID.'
         MEXIT
.E1      MNOTE 8,'NAME PARAMETER REQUIRED'
         MEXIT
.E2      MNOTE 8,'MESSAGE OPERAND REQUIRED'
         MEXIT
.E3      MNOTE 8,'INVALID ROUTE CODE PARAMETER'
         MEXIT
.E4      MNOTE 8,'INVALID LABEL RELATED PARAMETER'
         MEXIT
.E5      MNOTE 8,'DESCRIPTOR CODE INVALID'
         MEXIT
.E6      MNOTE 8,'ROUTCDE AND/OR DESC INVALID WITH MODE'
         MEXIT
.E7      MNOTE 8,'FORMAT INVALID, MUST BE MF=(G,ADR) OR MF=(G,(REG))'
         MEND
