         MACRO
&NAME    PARMS &SAVER
.*
&GNAME   SETC  'TSC'.'&SYSNDX'
.*--------------------------------------------------------------------*
.*       THIS CASUAL MACRO (PARMS) MOVES THE PARM DATA INTO A GIVEN   *
.*       AREA SPECIFIED BY THE USER.  IF NO PARM FIELD IS SUPPLIED,   *
.*       REGISTER 1 IS CLEARED TO ZERO.  OTHERWISE, R1 CONTAINS THE   *
.*       THE LENGTH OF THE PARM DATA PASSED.                          *
.* (TM)  TAURUS SOFTWARE CONSULTANTS - PETER MCFARLAND CONSULTANT     *
.*--------------------------------------------------------------------*
         L     15,4(,13)               ==> TO PREVIOUS SAVE AREA
         L     1,24(,15)               RE-LOAD R1 FORM SAVE AREA
         L     1,0(1)                  LOAD PARM ==>
         CLC   0(2,1),=H'0'            PARM OMITTED ?
         BE    &GNAME.X                YES, JUST EXIT W/R1 = 0
         SLR   14,14                   CLEAR WORK REGISTER
         LH    14,0(1)                 LOAD PARM COUNT
         LA    15,2(1)                 LOAD PARM ==>
         LR    1,14                    SAVE PARM LENGTH FOR USER
         BCTR  14,0                    MINUS ONE FOR MVC INST.
         EX    14,&GNAME.A             MOVE IN THE PARM DATA
         B     &GNAME.Z                ALL DONE
*
&GNAME.A MVC   &SAVER.(0),0(15)        EXECUTED INST.
&GNAME.X DS    0H
         SLR   1,1                     ZERO R1, NO PARM
&GNAME.Z DS    0H
         MEND
