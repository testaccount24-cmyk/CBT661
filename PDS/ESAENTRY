         MACRO
&NAME    ESAENTRY &PAR0=R10,&PAR1=R11,                                 +
               &SAVE=ALL,&BASEREGS=1,&WORKLEN=,&SP=3
.*
.* ESAENTRY USED IN CONJUNCTION WITH THE ESAEXIT MACRO, 06/27/89/$PMCF
.* Change Log: Added lower case eyecatcher & equates.   03/12/97/$PMCF
.* (TM)  TAURUS SOFTWARE CONSULTANTS - PETER MCFARLAND CONSULTANT
.*
         GBLC  &WRKLEN                 GETMAIN LENGTH
         GBLC  &SPOOL                  SUBPOOL
&NAME    CSECT
         USING &NAME,R15               ESTABLISH TEMP. ADDRESSING
         B     MSTART                  BRANCH AROUND IDENTFIERS
         DC    AL1(MODIDL)             ID LENGTH
MODID    DC    C' &NAME  '             MODULE NAME
         DC    C'&SYSDATE '            ASSEMBLY DATE
         DC    C'&SYSTIME '            ASSEMBLY TIME
         DC  C'Copyright (C) 1988-2099, Taurus Software Consultants,'
         DC  C' All Rights Reserved.'
MODIDL   EQU   *-MODID                 GENERATE ID LENGTH
*
         PRINT NOGEN
.*             Register Equates
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
.*             Equates
ZERO     EQU   0
ONE      EQU   1
NOT      EQU   X'FF'
D0       EQU   0
D4       EQU   4
D8       EQU   8
D12      EQU   12
D16      EQU   16
D20      EQU   20
D24      EQU   24
D28      EQU   28
         PRINT GEN
.* SAVE=ALL                    SAVE REGISTERS R14,R15,R0-R12
.* SAVE=R14                    SAVE REGISTER R14 IN R13
.*
MSTART   DS    0H
         AIF   ('&SAVE' NE 'ALL').SVCHK1
         STM   R14,R12,12(R13)         SAVE REGISTERS
         AGO   .PARM
.*
.SVCHK1  ANOP
         AIF   ('&SAVE' NE 'R14').SVERR
         LR    R13,R14                 SAVE REGISTER R14
         AGO   .PARM
.*
.SVERR   ANOP
         MNOTE 8,'SAVE PARAMETER INVALID'
         MEXIT
.*
.* PARM=REG                    SAVE PARM REGISTER (R1)
.*
.PARM    ANOP
         LR    &PAR0,R0                SAVE PARM REGISTER R0
         LR    &PAR1,R1                SAVE PARM REGISTER R1
         LR    R12,R15                 LOAD BASE REGISTER
         DROP  R15                     DROP TEMP. ADDRESSING
         AIF   ('&BASEREGS' EQ '1').BASE1
         AIF   ('&BASEREGS' EQ '2').BASE2
         AIF   ('&BASEREGS' EQ '3').BASE3
         MNOTE 8,'BASEREGS PARAMETER INVALID'
         MEXIT
.BASE1   ANOP
         USING &NAME,R12               ESTABLISH PERM. ADDRESSING
         AGO   .BASEX
.BASE2   ANOP
         LA    R11,4095(R12)
         LA    R11,1(R11)
         USING &NAME,R12,R11           ESTABLISH PERM. ADDRESSING
         AGO   .BASEX
.BASE3   ANOP
         LA    R11,4095(R12)
         LA    R11,1(R11)
         LA    R10,4095(R11)
         LA    R10,1(R10)
         USING &NAME,R12,R11,R10       ESTABLISH PERM. ADDRESSING
.BASEX   ANOP
&WRKLEN  SETC  '72'
&SPOOL   SETC  '&SP'
         AIF   ('&SAVE' EQ 'R14').EXIT
         AIF   (T'&WORKLEN EQ 'O').GET72
&WRKLEN  SETC  '&WORKLEN'
*        STORAGE OBTAIN,LENGTH=&WORKLEN,SP=&SPOOL,BNDRY=PAGE
         STORAGE OBTAIN,LENGTH=&WORKLEN,SP=&SPOOL,BNDRY=PAGE
         AGO   .CHAIN
.GET72   ANOP
*        STORAGE OBTAIN,LENGTH=72,SP=&SPOOL     GETMAIN SAVE AREA
         STORAGE OBTAIN,LENGTH=72,SP=&SPOOL     GETMAIN SAVE AREA
.CHAIN   ANOP
         XC    0(72,R1),0(R1)          CLEAR SAVE AREA
         ST    R13,4(,R1)              SAVE REGISTER R13
         ST    R1,8(,R13)              CHAIN SAVE AREAS
         LR    R13,R1                  USE NEW SAVE AREA
         AIF   (T'&WORKLEN EQ 'O').EXIT
         LA    R0,72(,R1)              WORK AREA ADDRESS
         LA    R1,&WORKLEN-72          WORK AREA LENGTH
         SLR   R15,R15                 NO OPERAND 2 / FILL =0
         MVCL  R0,R14                  ZERO OUT WORK AREA
.*
.EXIT    ANOP
         MEND
