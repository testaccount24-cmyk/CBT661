         MACRO
         CONVERT  &REG
         LCLC  &GNAME
         GBLB  &@@@CNVT
&GNAME   SETC  'TSC'.'&SYSNDX'
.*>>>>         CONVERT HEX CHARACTERS TO PRINTABLE FORMAT.....   $PMCF*
.*>>>>         REGISTER FORMAT IS AS FOLLOWS;                         *
.*>>>>         FIRST REGISTER POINTS TO THE RECEIVING FIELD           *
.*>>>>         SECOND REGISTER POINTS TO THE HEX FIELD                *
.*>>>>         THIRD REGISTER CONTAINS THE LENGTH OF HEX CHARACTERS   *
.*>>>>         TO BE CONVERTED.                                  $PMCF*
.*       TAURUS SOFTWARE CONSULTANTS - PETER MCFARLAND CONSULTANT
&GNAME.A DS    0H                 CONVERT HEX TO PRINTABLE CHARS.
         MVO   0(2,&REG(1)),0(1,&REG(2)) ADJUST OFFSET
         MVN   1(1,&REG(1)),0(&REG(2)) SET NUMERIC CHAR.
         NI    1(&REG(1)),X'0F'        FLIP UNWANTED BITS OFF
         TR    0(2,&REG(1)),@@@CNVRT   TRANSLATE THE BYTE
         LA    &REG(1),2(0,&REG(1))    BUMP UP
         LA    &REG(2),1(0,&REG(2))      THE REGISTERS.
         BCT   &REG(3),&GNAME.A        CONVERT AGAIN IF REG. NZERO
         AIF   (&@@@CNVT).NOTABL       SKIP TRAN-TABLE AND BRANCH
.*                                     IF WE ALREADY HAVE ONE
&@@@CNVT SETB  1
         B     &GNAME.B                BRANCH AROUND TR TABLE
@@@CNVRT DC    C'0123456789ABCDEF'     CHARACTER STRING FOR TR INST.
&GNAME.B DS    0H
.NOTABL  ANOP
         MEND
