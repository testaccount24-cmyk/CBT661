         MACRO
         $EX   &REG,&INST
.*--------------------------------------------------------------------*
.*
.*  THIS MACRO WILL GENERATE AN EXECUTE INSTRUCTION, AND THEN TABLE
.*  FOR LATER USE THE EXECUTED INSTRUCTION WHICH IS OF THE FORM:
.*      (EXINST,OP1,OP2,OP3)                                   05/15/83
.*
.*  THE EXECUTED INSTRUCTIONS ARE GENERATED WHEN &REG IS 'GENERATE'
.*  TAURUS SOFTWARE CONSULTANTS - PETER MCFARLAND CONSULTANT
.*
.*--------------------------------------------------------------------*
         GBLA  &$EX#  COUNT OF SAVED EXECUTED INSTRUCTIONS
         GBLC  &$EXIN(50)  SAVED INSTRUCTION OPCODES
         GBLC  &$EXOP(50)  SAVED INSTRUCTION OPERANDS
         GBLC  &$EXCS(50)  SAVED INSTRUCTION CSECTS
         LCLA  &M,&N  INTERNAL WORK VARIABLES
         LCLC  &OPCOD,&OPNDS,&CSAVE
         AIF   ('&REG' EQ 'GENERATE').GEN EXECUTED INSTRUCTIONS
         AIF   (T'&REG NE 'O').RSPEC
         MNOTE 8,'REGISTER INVALID'
         MEXIT
.*
.RSPEC   ANOP
         AIF   (N'&INST GE 3).INSTS                           05/15/83
         MNOTE 8,'INSTRUCTION INVALID'
         MEXIT
.*
.INSTS   ANOP
&OPCOD   SETC  '&INST(1)'                                     05/15/83
&M       SETA  K'&INST-K'&INST(1)-3 LENGTH WITHOUT OPCODE AND PARENS5/
&OPNDS   SETC  '&INST'(K'&INST(1)+3,&M) EXTRACT OPERANDS      05/15/83
&N       SETA  1
.DUPTEST ANOP
         AIF   (&N GT &$EX#).NOK
         AIF  ('&$EXIN(&N)' EQ '&OPCOD' AND '&$EXOP(&N)' EQ '&OPNDS'   +
               AND '&$EXCS(&N)' EQ '&SYSECT').GENEX
&N       SETA  &N+1
         AIF   (&N LE 50).DUPTEST
         MNOTE 8,'INSTRUCTION DUPLICATED'
         MEXIT
.*
.NOK     ANOP
.*             NO DUPLICATE FOUND, SO ADD THIS INSTRUCTION TO TABLE
&$EXIN(&N) SETC '&OPCOD'
&$EXOP(&N) SETC '&OPNDS'
&$EXCS(&N) SETC '&SYSECT'
&$EX#      SETA &N
.GENEX   ANOP
         EX    &REG,@@EX&N
         MEXIT
.*
.*
.*   THIS PORTION IS USED TO GENERATE THE SAVED EXECUTED INSTRUCTIONS
.*
.*
.GEN     ANOP
&CSAVE   SETC  '&SYSECT'
&N       SETA  1
.GENXT   ANOP
         AIF   (&N GT &$EX#).EXIT
         AIF   ('&CSAVE' EQ '&$EXCS(&N)').NOCSCT
&CSAVE   SETC  '&$EXCS(&N)'
&CSAVE   CSECT
.NOCSCT  ANOP
@@EX&N   &$EXIN(&N)  &$EXOP(&N)
&N       SETA  &N+1
         AGO   .GENXT
.EXIT    ANOP
&$EX#    SETA  0
&SYSECT  CSECT
         MEND
